--CODE BY Hubert Garavel

module dice is

channel THROW is
   (PROB: real)
   -- assert PROB = 0.5
end channel



process MAIN [HEAD, TAIL, DICE1, DICE2, DICE3, DICE4, DICE5, DICE6: THROW] is
   loop
      alt 
         HEAD (0.5);
         alt
             HEAD (0.5);
             alt
                 HEAD (0.5);
                 loop DICE1(1.0) end loop -- infinite loop
             []
                TAIL (0.5);
                loop DICE2 (1.0) end loop
             end alt
         []
             TAIL (0.5);
             alt
                 HEAD (0.5);
                 loop DICE3 (1.0) end loop
             []
                TAIL (0.5);
                loop DICE4 (1.0) end loop
             end alt
         end alt
      []
         TAIL (0.5);
         alt
             HEAD (0.5);
             alt
                 HEAD (0.5);
                 loop DICE5 (1.0) end loop
             []
                TAIL (0.5);
                loop DICE6 (1.0) end loop
             end alt
         []
             TAIL (0.5)
             -- in the two remaining states, loop back to the initial state
         end alt
      end alt
   end loop
end process

end module

